import org.cyclonedx.model.OrganizationalContact
import org.cyclonedx.model.OrganizationalEntity;

plugins {
  alias(libs.plugins.vanniktech.maven.publish)
  alias(libs.plugins.cyclonedx.bom)
  id 'application'
}

application {
  mainClass = 'tech.bison.datalift.commandline.Main'
}

tasks.cyclonedxDirectBom {
  includeConfigs = ["runtimeClasspath"]
  def contact = new OrganizationalContact()
  contact.name = "Bison Schweiz AG"

  def entity = new OrganizationalEntity()
  entity.name = "Bison Schweiz AG"
  entity.addContact(contact)

  organizationalEntity = entity
}

mavenPublishing {
  publishToMavenCentral()
  signAllPublications()
  coordinates("tech.bison", "commercetools-datalift-commandline", "${project.version}")

  pom {
    name = "commercetools Datalift command line interface"
    description = "Command line interface for commercetools Datalift."
    inceptionYear = "2024"
    url = "https://github.com/BisonSchweizAG/commercetools-datalift"
    licenses {
      license {
        name = "The Apache License, Version 2.0"
        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
        distribution = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }
    }
    developers {
      developer {
        id = "BisonSchweizAG"
        name = "Bison Schweiz AG"
        url = "https://github.com/BisonSchweizAG"
      }
    }
    scm {
      url = "https://github.com/BisonSchweizAG/commercetools-datalift"
      connection = "scm:git:git://github.com/BisonSchweizAG/commercetools-datalift.git"
      developerConnection = "scm:git:ssh://git@github.com/BisonSchweizAG/commercetools-datalift.git"
    }
  }
}

afterEvaluate {
  publishing {
    publications {
      maven(MavenPublication) {
        artifact(tasks.distTar) {
          classifier = "bin"
          extension = "tar.gz"
        }
        artifact(file("$buildDir/reports/cyclonedx-direct/bom.json")) {
          classifier = "sbom"
          extension = "json"
        }
      }
    }
  }
}

dependencies {
  implementation project(':datalift-core')
  implementation libs.picocli
  implementation libs.slf4j

  testImplementation libs.junit.jupiter.api
  testImplementation libs.junit.jupiter.params
  testImplementation libs.mockito.junit.jupiter

  testRuntimeOnly libs.junit.jupiter.engine
  testRuntimeOnly libs.junit.platform.launcher
  runtimeOnly libs.commercetools.okhttp.client4
  runtimeOnly libs.logback
}

test {
  useJUnitPlatform()
}
