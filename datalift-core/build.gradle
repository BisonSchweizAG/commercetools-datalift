plugins {
  id 'org.jreleaser' version '1.13.1'
}
dependencies {
  implementation libs.commercetools.java.api
  implementation libs.commercetools.java.importapi
  implementation libs.jackson.core
  implementation libs.reflections
  implementation libs.slf4j


  testImplementation libs.junit.jupiter.api
  testImplementation libs.junit.jupiter.params
  testImplementation libs.mockito.junit.jupiter
  testImplementation libs.assertj.core
  testImplementation libs.testcontainers.mockserver
  testImplementation libs.mockserver.client
  testImplementation libs.testcontainers.junit.jupiter

  testRuntimeOnly libs.junit.jupiter.engine
  testRuntimeOnly libs.logback
  runtimeOnly libs.commercetools.okhttp.client4
}

jreleaser {
  project {
    gitRootSearch = true
    description = 'Datalift is a tool to automate data migrations for commercetools based applications.'
    authors = ['BisonSchweizAG']
    license = 'Apache-2.0'
    inceptionYear = '2024'
    website = 'https://github.com/BisonSchweizAG/datalift'
    docsUrl = 'https://github.com/BisonSchweizAG/datalift'
    version = '0.9.0'
    stereotype = 'NONE'
  }

  assemble {
    javaArchive {
      dataliftcore {
        active = 'ALWAYS'
        stereotype = 'NONE'
        formats = [
            'ZIP',
            'TGZ'
        ]
      }
    }
  }

  release {
    github {
      repoOwner = 'BisonSchweizAG'
      overwrite = true
    }
  }
  distributions {
    dataliftcore {
      artifact {
        path = 'build/libs/{{distributionName}}-{{projectVersion}}.zip'
      }
    }
  }
}

test {
  testLogging {
    events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
  }
  useJUnitPlatform() {
    filter {
      includeTestsMatching "*Test"
      excludeTestsMatching "*IT"
    }
  }
}

tasks.register("integrationTest", Test) {
  useJUnitPlatform {
    filter {
      includeTestsMatching '*IT'
    }
  }
}
