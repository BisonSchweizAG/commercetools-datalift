dependencies {
  implementation libs.commercetools.java.api
  implementation libs.commercetools.java.importapi
  implementation libs.jackson.core
  implementation libs.reflections
  implementation libs.slf4j


  testImplementation libs.junit.jupiter.api
  testImplementation libs.junit.jupiter.params
  testImplementation libs.mockito.junit.jupiter
  testImplementation libs.assertj.core
  testImplementation libs.testcontainers.mockserver
  testImplementation libs.mockserver.client
  testImplementation libs.testcontainers.junit.jupiter

  testRuntimeOnly libs.junit.jupiter.engine
  testRuntimeOnly libs.logback
  runtimeOnly libs.commercetools.okhttp.client4
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = 'datalift-core'
      version = '0.9-SNAPSHOT'
      group = 'io.github.studix'
      pom {
        name = 'Datalift Core'
        description = 'Core artifact of datalift, a tool for automating data migrations of commercetools applications'
        url = 'https://github.com/BisonSchweizAG/datalift'
        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'studix'
            name = 'Marco Studer'
            email = 'marco.studer@bison-group.com'
          }
        }
        scm {
          connection = 'scm:git:git:github.com/BisonSchweizAG/datalift.git'
          developerConnection = 'scm:git:ssh://github.com/BisonSchweizAG/datalift.git'
          url = 'https://github.com/BisonSchweizAG/datalift'
        }
      }
      from components.java
    }
  }
  repositories {
    maven {
      name = "OSSRH"
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = project.properties["ossrhUsername"]
        password = project.properties["ossrhPassword"]
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

test {
  testLogging {
    events "PASSED", "SKIPPED", "FAILED", "STANDARD_OUT", "STANDARD_ERROR"
  }
  useJUnitPlatform() {
    filter {
      includeTestsMatching "*Test"
      excludeTestsMatching "*IT"
    }
  }
}

tasks.register("integrationTest", Test) {
  useJUnitPlatform {
    filter {
      includeTestsMatching '*IT'
    }
  }
}
